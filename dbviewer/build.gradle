apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'net.zetetic:android-database-sqlcipher:4.3.0'
}

group = "damiao.hr"
version = "0.0.1"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                description 'An Android Real-Time Database Viewer'
                name 'DBViewer'  // 可默认，项目描述
                url 'https://github.com/heavenrobotxu/DbViewer'
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //  可默认，开发者信息
                        id 'damiaomiao'
                        name 'xudamiao'
                        email 'heavenrobotxu@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/heavenrobotxu/DbViewer.git'
                    developerConnection 'https://github.com/heavenrobotxu/DbViewer.git'
                    url 'https://github.com/heavenrobotxu/DbViewer'
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//这个是和你bintray上面创建的package(项目)是对应的
bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.pwd')
    configurations = ['archives']
    pkg {
        repo = "DBViewer"  //发布到Bintray的那个仓库里，默认账户有四个库，我们这里上传到maven库
        name = "dbviewer"  //需更改，这个名字就是你在Bintray上创建Package的名字，这里会查询，如果此包不存在就会创建一个。
        userOrg = 'heavenrobotxu'    //组织名称 organization
        websiteUrl = 'https://github.com/heavenrobotxu/DbViewer'
        vcsUrl = 'https://github.com/heavenrobotxu/DbViewer.git'
        licenses = ["Apache-2.0"]
        publish = true
    }
}
